=LET(
dataMed;M6;
status;R6;
hoje;HOJE();

ehBacklog;OU(status="Backlog";status="em Andamento");
ehAvaliação;status="em Avaliação";
statusValido;OU(ehBacklog;ehAvaliação);

diaCorte;SE(ehBacklog;20;25);

fechamentoMed;
DIATRABALHO.INTL(
DATA(ANO(dataMed);MÊS(dataMed)+SE(DIA(dataMed)>diaCorte;1;0)+1;1);
-1;"0000011"
);

fechamentoAtual;
DIATRABALHO.INTL(
DATA(
ANO(hoje);
MÊS(hoje)+SE(
OU(E(ehBacklog;DIA(hoje)>20);E(ehAvaliação;DIA(hoje)>25));
1;0
)+1;
1
);
-1;"0000011"
);

diferenca;
(ANO(fechamentoAtual)-ANO(fechamentoMed))*12+
MÊS(fechamentoAtual)-MÊS(fechamentoMed);

SE(
dataMed="";
"";
SE(
status="Cancelado";
"HOLD";
SE(
NÃO(statusValido);
"";
SE(
diferenca=1;
TEXTO(fechamentoAtual;"mmm/aa");
SE(diferenca>=2;"Alerta HOLD";"")
)
)
)
)
)
